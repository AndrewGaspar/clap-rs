pat=''
args=''
flags=''
threads='4'
features=''

@update-contributors:
	githubcontrib --help 2>/dev/null || echo 'githubcontrib not found, see https://github.com/mgechev/github-contributors-list' && false
	echo 'Removing old CONTRIBUTORS.md'
	mv CONTRIBUTORS.md CONTRIBUTORS.md.bak
	echo 'Downloading a list of new contributors'
	echo "the following is a list of contributors:" > CONTRIBUTORS.md
	echo "" >> CONTRIBUTORS.md
	echo "" >> CONTRIBUTORS.md
	githubcontrib --owner kbknapp --repo clap-rs --sha master --cols 6 --format md --showlogin true --sortBy contributions --sortOrder desc >> CONTRIBUTORS.md
	echo "" >> CONTRIBUTORS.md
	echo "" >> CONTRIBUTORS.md
	echo "This list was generated by [mgechev/github-contributors-list](https://github.com/mgechev/github-contributors-list)" >> CONTRIBUTORS.md
	rm CONTRIBUTORS.md.bak

run-test TEST:
	RUST_TEST_THREADS={{threads}} {{flags}} cargo test --features "{{features}}" --test {{TEST}} {{pat}} -- {{args}}

run-tests:
	RUST_TEST_THREADS={{threads}} {{flags}} cargo test --features "{{features}}" {{pat}} -- {{args}}

debug TEST:
	RUST_TEST_THREADS={{threads}} {{flags}} cargo test --features "debug {{features}}" --test {{TEST}} {{pat}} -- {{args}}

bench: set-nightly
	cargo bench 
	just remove-nightly

set-nightly:
	rustc -V | grep 'nightly' || rustup override add nightly

remove-nightly:
	rustc -V | grep 'stable' || rustup override remove

lint: set-nightly
	cargo build --features lints
	just remove-nightly

clean:
	cargo clean
	find . -type f -name "*.orig" -exec rm {} \;
	find . -type f -name "*.bk" -exec rm {} \;
	find . -type f -name ".*~" -exec rm {} \;

count-errors:
    cargo build 2>&1 | grep -e 'error[:\[]' | wc -l

find-errors:
    cargo build 2>&1 | grep -e '--> [^:]*' -o | sort | uniq -c
